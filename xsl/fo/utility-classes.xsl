<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:fo="http://www.w3.org/1999/XSL/Format"
  xmlns:fox="http://xmlgraphics.apache.org/fop/extensions"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:opentopic-func="http://www.idiominc.com/opentopic/exsl/function"
  xmlns:dita-ot="http://dita-ot.sourceforge.net/ns/201007/dita-ot"
  exclude-result-prefixes="xs opentopic-func dita-ot"
  version="2.0"
>

  <xsl:param name="BOOTSTRAP_ICONS_INCLUDE" select="'yes'"/>
  
  <!-- Helper Template to retrieve settings from the $bootstrap-settings map with a fallback -->
  <xsl:template name="getBootstrapSetting">
    <xsl:param name="name"/>
    <xsl:param name="default"/>
    <xsl:variable name="val" select="$bootstrap-settings/entry[@name = $name]"/>
    <xsl:choose>
      <xsl:when test="$val != ''"><xsl:value-of select="$val"/></xsl:when>
      <xsl:otherwise><xsl:value-of select="$default"/></xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- Map DITA Note Type to Bootstrap Theme -->
  <xsl:template name="getNoteTheme">
    <xsl:param name="type" select="'note'"/>
    <xsl:choose>
      <xsl:when test="$type = 'note' or $type = 'notice' or $type = 'remember'">info</xsl:when>
      <xsl:when test="$type = 'tip' or $type = 'fastpath'">success</xsl:when>
      <xsl:when test="$type = 'important'">primary</xsl:when>
      <xsl:when test="$type = 'warning' or $type = 'caution' or $type = 'restriction' or $type = 'trouble'">warning</xsl:when>
      <xsl:when test="$type = 'danger'">danger</xsl:when>
      <xsl:otherwise>secondary</xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- Retrieve the computed value of a specific attribute from an attribute-set -->
  <xsl:template name="getBootstrapAttrValue">
    <xsl:param name="attrSet"/>
    <xsl:param name="attrName" select="'color'"/>
    <xsl:param name="path" select="'../../cfg/fo/attrs/bootstrap-attr.xsl'"/>
    <xsl:variable name="custom-attr" select="if (doc-available('cfg:fo/attrs/custom.xsl')) then document('cfg:fo/attrs/custom.xsl')//xsl:attribute-set[@name = $attrSet]/xsl:attribute[@name = $attrName] else ()"/>
    <xsl:variable name="theme-attr" select="if (not($custom-attr) and doc-available('cfg:fo/attrs/dita-ot.xsl')) then document('cfg:fo/attrs/dita-ot.xsl')//xsl:attribute-set[@name = $attrSet]/xsl:attribute[@name = $attrName] else ()"/>
    <xsl:variable name="attr" select="($custom-attr, $theme-attr, document($path)//xsl:attribute-set[@name = $attrSet]/xsl:attribute[@name = $attrName])[1]"/>
    <xsl:choose>
      <xsl:when test="$attr/xsl:value-of">
        <xsl:variable name="select" select="$attr/xsl:value-of/@select"/>
        <xsl:variable name="varName" select="if (starts-with($select, '$')) then substring-after($select, '$') else $select"/>
        <xsl:value-of select="$bootstrap-settings/entry[@name = $varName]"/>
      </xsl:when>
      <xsl:otherwise>
        <xsl:value-of select="$attr"/>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- Reflection Template for Bootstrap Attribute Sets -->
  <xsl:template name="processBootstrapAttrSetReflection">
    <xsl:param name="attrSet"/>
    <xsl:param name="path" select="'../../cfg/fo/attrs/bootstrap-attr.xsl'"/>

    <xsl:variable name="custom-attrs" select="if (doc-available('cfg:fo/attrs/custom.xsl')) then document('cfg:fo/attrs/custom.xsl')//xsl:attribute-set[@name = $attrSet]/xsl:attribute else ()"/>
    <xsl:variable name="theme-attrs" select="if (not($custom-attrs) and doc-available('cfg:fo/attrs/dita-ot.xsl')) then document('cfg:fo/attrs/dita-ot.xsl')//xsl:attribute-set[@name = $attrSet]/xsl:attribute else ()"/>
    <xsl:variable name="attrs" select="(document($path)//xsl:attribute-set[@name = $attrSet]/xsl:attribute, $theme-attrs, $custom-attrs)"/>

    <xsl:for-each select="$attrs">
      <xsl:attribute name="{@name}">
        <xsl:for-each select="node()">
          <xsl:choose>
            <xsl:when test="self::xsl:value-of">
              <xsl:variable name="select" select="@select"/>
              <xsl:variable
                name="varName"
                select="if (starts-with($select, '$')) then substring-after($select, '$') else $select"
              />
              <xsl:value-of select="$bootstrap-settings/entry[@name = $varName]"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="."/>
            </xsl:otherwise>
          </xsl:choose>
        </xsl:for-each>
      </xsl:attribute>
    </xsl:for-each>
  </xsl:template>

  <!-- Process @width attribute -->
  <xsl:template name="processBootstrapWidth">
    <xsl:param name="attrValue"/>
    <xsl:if test="$attrValue">
      <xsl:call-template name="processBootstrapAttrSetReflection">
        <xsl:with-param name="attrSet" select="concat('w-', $attrValue)"/>
      </xsl:call-template>
    </xsl:if>
  </xsl:template>

  <!-- Process @border attribute -->
  <xsl:template name="processBootstrapBorder">
    <xsl:param name="attrValue"/>
    <xsl:if test="$attrValue">
      <xsl:for-each select="tokenize(normalize-space($attrValue), ' ')">
        <xsl:variable name="token" select="."/>

        <!-- Apply base border style for numeric thickness outside the variable to avoid Saxon error -->
        <xsl:if test="string(number($token)) != 'NaN'">
          <xsl:call-template name="processBootstrapAttrSetReflection">
            <xsl:with-param name="attrSet" select="'border'"/>
          </xsl:call-template>
        </xsl:if>

        <xsl:variable name="attrSetName">
          <xsl:choose>
            <xsl:when test="$token = 'yes' or $token = 'true' or $token = 'border'">border</xsl:when>
            <xsl:when test="starts-with($token, 'border-')">
              <xsl:value-of select="$token"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="concat('border-', $token)"/>
            </xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        
        <xsl:call-template name="processBootstrapAttrSetReflection">
          <xsl:with-param name="attrSet" select="$attrSetName"/>
        </xsl:call-template>
      </xsl:for-each>
    </xsl:if>
  </xsl:template>

  <!-- Process @frame attribute for Figures -->
  <xsl:template name="processBootstrapFrame">
    <xsl:param name="attrValue"/>
    <xsl:if test="$attrValue">
      <xsl:variable name="processedValue">
        <xsl:choose>
          <xsl:when test="$attrValue = 'all'">border</xsl:when>
          <xsl:when test="$attrValue = 'sides'">start end</xsl:when>
          <xsl:when test="$attrValue = 'top'">top</xsl:when>
          <xsl:when test="$attrValue = 'bottom'">bottom</xsl:when>
          <xsl:when test="$attrValue = 'topbot'">top bottom</xsl:when>
        </xsl:choose>
      </xsl:variable>
      <xsl:if test="normalize-space($processedValue) != ''">
        <xsl:call-template name="processBootstrapBorder">
          <xsl:with-param name="attrValue" select="$processedValue"/>
        </xsl:call-template>
        <!-- Effectively an additional padded border - using p-3 / 12pt -->
        <xsl:attribute name="padding"><xsl:value-of select="$bootstrap-spacing-3"/></xsl:attribute>
      </xsl:if>
    </xsl:if>
  </xsl:template>

  <!-- Process @bordercolor attribute -->
  <xsl:template name="processBootstrapBorderColor">
    <xsl:param name="attrValue"/>
    <xsl:if test="$attrValue">
      <xsl:variable name="isZeroWidth" select="normalize-space($bootstrap-border-width) = ('0', '0pt', '0px', '0in', '0mm', '0cm', '0.0pt', '0.0px')"/>
      <xsl:if test="not($isZeroWidth)">
        <xsl:attribute name="border-style">solid</xsl:attribute>
        <xsl:attribute name="border-width"><xsl:value-of select="$bootstrap-border-width"/></xsl:attribute>
      </xsl:if>
      <xsl:call-template name="processBootstrapAttrSetReflection">
        <xsl:with-param name="attrSet" select="concat('border-', $attrValue)"/>
      </xsl:call-template>
    </xsl:if>
  </xsl:template>


  <!-- Process @dir attribute for RTL/LTR direction -->
  <xsl:template name="processBootstrapDirection">
    <xsl:variable name="direction">
        <xsl:choose>
            <xsl:when test="@dir"><xsl:value-of select="@dir"/></xsl:when>
            <xsl:when test="ancestor::*[@dir]"><xsl:value-of select="ancestor::*[@dir][1]/@dir"/></xsl:when>
            <xsl:otherwise/>
        </xsl:choose>
    </xsl:variable>

    <xsl:choose>
      <xsl:when test="$direction = 'rtl'">
        <xsl:attribute name="writing-mode">rl-tb</xsl:attribute>
        <xsl:attribute name="direction">rtl</xsl:attribute>
        <xsl:attribute name="text-align">right</xsl:attribute>
        <xsl:attribute name="text-align-last">right</xsl:attribute>
      </xsl:when>
      <xsl:when test="$direction = 'ltr'">
        <xsl:attribute name="writing-mode">lr-tb</xsl:attribute>
        <xsl:attribute name="direction">ltr</xsl:attribute>
        <xsl:attribute name="text-align">left</xsl:attribute>
        <xsl:attribute name="text-align-last">left</xsl:attribute>
      </xsl:when>
    </xsl:choose>
  </xsl:template>

  <xsl:template name="processBootstrapRounded">
    <xsl:param name="attrValue"/>
    <xsl:if test="$attrValue">
      <xsl:variable name="level" select="if ($attrValue = ('yes', 'true')) then '' else concat('-', $attrValue)"/>
      <xsl:variable name="varName" select="concat('bootstrap-rounded', $level)"/>
      <xsl:variable name="val">
        <xsl:call-template name="getBootstrapSetting">
          <xsl:with-param name="name" select="$varName"/>
        </xsl:call-template>
      </xsl:variable>

      <xsl:if test="not(normalize-space($val) = ('0', '0pt', '0px', '0in', '0mm', '0cm', '0.0pt', '0.0px'))">
        <xsl:variable name="attrSetName">
          <xsl:choose>
            <xsl:when test="$attrValue = 'yes' or $attrValue = 'true'">rounded</xsl:when>
            <xsl:otherwise><xsl:value-of select="concat('rounded-', $attrValue)"/></xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:call-template name="processBootstrapAttrSetReflection">
          <xsl:with-param name="attrSet" select="$attrSetName"/>
        </xsl:call-template>
      </xsl:if>
    </xsl:if>
  </xsl:template>

  <!-- Process @margin and @padding attributes -->
  <xsl:template name="processBootstrapSpacing">
    <xsl:param name="attrValue"/>
    <xsl:param name="prefix"/> <!-- 'p' or 'm' -->
    <xsl:if test="$attrValue">
      <xsl:for-each select="tokenize(normalize-space($attrValue), ' ')">
        <xsl:variable name="token" select="."/>
        <xsl:variable name="attrSetName">
          <xsl:choose>
            <xsl:when test="$token = 'auto'">
              <xsl:value-of select="concat($prefix, '-auto')"/>
            </xsl:when>
            <xsl:when test="string-length($token) = 1">
              <xsl:value-of select="concat($prefix, '-', $token)"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="concat($prefix, substring($token, 1, 1), '-', substring($token, 2))"/>
            </xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:call-template name="processBootstrapAttrSetReflection">
          <xsl:with-param name="attrSet" select="$attrSetName"/>
        </xsl:call-template>
      </xsl:for-each>
    </xsl:if>
  </xsl:template>

  <!-- Process @outputclass attribute for Bootstrap classes -->
  <xsl:template name="processBootstrapOutputClass">
    <xsl:param name="attrValue"/>
    <xsl:if test="$attrValue">
      <xsl:variable name="tokens" select="tokenize(normalize-space($attrValue), ' ')"/>
      
      <!-- Pass 1: Background and other utilities -->
      <xsl:for-each select="$tokens">
        <xsl:variable name="token" select="."/>
        <xsl:choose>
          <xsl:when test="starts-with($token, 'bg-')">
            <xsl:call-template name="processBootstrapAttrSetReflection">
              <xsl:with-param name="attrSet" select="concat('__bg__', substring-after($token, 'bg-'))"/>
            </xsl:call-template>
          </xsl:when>
          <xsl:when
            test="$token = 'border' or $token = 'border-top' or $token = 'border-bottom' or $token = 'border-start' or $token = 'border-end' or 
                        starts-with($token, 'border-') or starts-with($token, 'rounded-') or $token = 'rounded' or
                        starts-with($token, 'w-') or starts-with($token, 'p-') or starts-with($token, 'm-') or
                        starts-with($token, 'px-') or starts-with($token, 'py-') or starts-with($token, 'pt-') or starts-with($token, 'pb-') or starts-with($token, 'ps-') or starts-with($token, 'pe-') or
                        starts-with($token, 'mx-') or starts-with($token, 'my-') or starts-with($token, 'mt-') or starts-with($token, 'mb-') or starts-with($token, 'ms-') or starts-with($token, 'me-') or
                        $token = 'h1' or $token = 'h2' or $token = 'h3' or $token = 'h4' or $token = 'h5' or $token = 'h6' or
                        starts-with($token, 'display-')"
          >
            <xsl:call-template name="processBootstrapAttrSetReflection">
              <xsl:with-param name="attrSet" select="$token"/>
            </xsl:call-template>
          </xsl:when>
          <!-- Text Align utilities (New) -->
          <xsl:when test="$token = 'text-start'"><xsl:attribute name="text-align">left</xsl:attribute></xsl:when>
          <xsl:when test="$token = 'text-center'"><xsl:attribute name="text-align">center</xsl:attribute></xsl:when>
          <xsl:when test="$token = 'text-end'"><xsl:attribute name="text-align">right</xsl:attribute></xsl:when>
        </xsl:choose>
      </xsl:for-each>

      <!-- Pass 2: Text color -->
      <xsl:for-each select="$tokens">
        <xsl:variable name="token" select="."/>
        <xsl:if
          test="starts-with($token, 'text-') and not($token = 'text-start' or $token = 'text-center' or $token = 'text-end')"
        >
          <xsl:call-template name="processBootstrapAttrSetReflection">
            <xsl:with-param name="attrSet" select="concat('__color__', substring-after($token, 'text-'))"/>
          </xsl:call-template>
        </xsl:if>
      </xsl:for-each>
    </xsl:if>
  </xsl:template>

  <!-- Titles within colored components -->
  <xsl:template match="*[contains(@class, ' topic/title ')][ancestor::*[contains(@class, ' topic/note ')] or ancestor::*[contains(@class, ' bootstrap-d/alert ')] or ancestor::*[contains(@class, ' topic/section ') or contains(@class, ' topic/div ') or contains(@class, ' topic/bodydiv ')][@color]]" priority="6">
      <xsl:variable name="theme">
        <xsl:choose>
          <xsl:when test="ancestor::*[contains(@class, ' topic/note ')]/@color"><xsl:value-of select="ancestor::*[contains(@class, ' topic/note ')]/@color"/></xsl:when>
          <xsl:when test="ancestor::*[contains(@class, ' topic/note ')]">
            <xsl:call-template name="getNoteTheme">
               <xsl:with-param name="type" select="(ancestor::*[contains(@class, ' topic/note ')]/@type, 'note')[1]"/>
            </xsl:call-template>
          </xsl:when>
          <xsl:when test="ancestor::*[contains(@class, ' bootstrap-d/alert ')]"><xsl:value-of select="(ancestor::*[contains(@class, ' bootstrap-d/alert ')]/@color, 'secondary')[1]"/></xsl:when>
          <xsl:when test="ancestor::*[contains(@class, ' topic/section ') or contains(@class, ' topic/div ') or contains(@class, ' topic/bodydiv ')]"><xsl:value-of select="ancestor::*[contains(@class, ' topic/section ') or contains(@class, ' topic/div ') or contains(@class, ' topic/bodydiv ')][1]/@color"/></xsl:when>
        </xsl:choose>
      </xsl:variable>

      <xsl:variable name="subtleColor">
         <xsl:if test="$theme != ''">
            <xsl:call-template name="getBootstrapAttrValue">
               <xsl:with-param name="attrSet" select="concat('__bg__', $theme, '-subtle')"/>
            </xsl:call-template>
         </xsl:if>
      </xsl:variable>

      <xsl:choose>
         <xsl:when test="parent::*[contains(@class, ' topic/example ')]">
            <fo:block xsl:use-attribute-sets="example.title">
               <xsl:if test="$subtleColor != ''"><xsl:attribute name="color"><xsl:value-of select="$subtleColor"/></xsl:attribute></xsl:if>
               <xsl:call-template name="commonattributes"/>
               <xsl:apply-templates/>
            </fo:block>
         </xsl:when>
         <xsl:otherwise>
            <fo:block xsl:use-attribute-sets="section.title">
               <xsl:if test="$subtleColor != ''"><xsl:attribute name="color"><xsl:value-of select="$subtleColor"/></xsl:attribute></xsl:if>
               <xsl:call-template name="commonattributes"/>
               <xsl:apply-templates/>
            </fo:block>
         </xsl:otherwise>
      </xsl:choose>
  </xsl:template>

  <!-- Baseline Section and Div Support -->
  <xsl:template
    match="*[contains(@class, ' topic/section ') or 
                         contains(@class, ' topic/div ') or 
                         contains(@class, ' topic/bodydiv ')]"
  >
    <fo:block>
      <xsl:call-template name="commonattributes"/>
      <xsl:if test="@color">
        <xsl:call-template name="processBootstrapAttrSetReflection">
          <xsl:with-param name="attrSet" select="concat('__bg__', @color)"/>
        </xsl:call-template>
        <xsl:if test="not(@padding)">
          <xsl:attribute name="padding">5pt</xsl:attribute>
        </xsl:if>
      </xsl:if>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@padding"/>
        <xsl:with-param name="prefix" select="'p'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@margin"/>
        <xsl:with-param name="prefix" select="'m'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorder">
        <xsl:with-param name="attrValue" select="@border"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapWidth">
        <xsl:with-param name="attrValue" select="@width"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorderColor">
        <xsl:with-param name="attrValue" select="@bordercolor"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapRounded">
        <xsl:with-param name="attrValue" select="@rounded"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapOutputClass">
        <xsl:with-param name="attrValue" select="@outputclass"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapDirection"/>
      <xsl:apply-templates/>
    </fo:block>
  </xsl:template>

  <!-- Inline Ph Support -->
  <xsl:template match="*[contains(@class, ' topic/ph ')]">
    <fo:inline>
      <xsl:call-template name="commonattributes"/>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@padding"/>
        <xsl:with-param name="prefix" select="'p'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@margin"/>
        <xsl:with-param name="prefix" select="'m'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorder">
        <xsl:with-param name="attrValue" select="@border"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorderColor">
        <xsl:with-param name="attrValue" select="@bordercolor"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapRounded">
        <xsl:with-param name="attrValue" select="@rounded"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapOutputClass">
        <xsl:with-param name="attrValue" select="@outputclass"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapDirection"/>
      <xsl:apply-templates/>
    </fo:inline>
  </xsl:template>

  <!-- Paragraph Support -->
  <xsl:template match="*[contains(@class, ' topic/p ')]">
    <fo:block xsl:use-attribute-sets="p">
      <xsl:call-template name="commonattributes"/>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@padding"/>
        <xsl:with-param name="prefix" select="'p'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@margin"/>
        <xsl:with-param name="prefix" select="'m'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorder">
        <xsl:with-param name="attrValue" select="@border"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapWidth">
        <xsl:with-param name="attrValue" select="@width"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorderColor">
        <xsl:with-param name="attrValue" select="@bordercolor"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapRounded">
        <xsl:with-param name="attrValue" select="@rounded"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapOutputClass">
        <xsl:with-param name="attrValue" select="@outputclass"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapDirection"/>
      <xsl:apply-templates/>
    </fo:block>
  </xsl:template>

  <!-- Blockquote (lq) Support -->
  <xsl:template match="*[contains(@class, ' topic/lq ')]">
    <fo:block margin-bottom="12pt">
      <xsl:call-template name="commonattributes"/>
      <xsl:if test="@color">
        <xsl:call-template name="processBootstrapAttrSetReflection">
          <xsl:with-param name="attrSet" select="concat('__bg__', @color)"/>
        </xsl:call-template>
        <xsl:if test="not(@padding)">
          <xsl:attribute name="padding">5pt</xsl:attribute>
        </xsl:if>
      </xsl:if>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@padding"/>
        <xsl:with-param name="prefix" select="'p'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@margin"/>
        <xsl:with-param name="prefix" select="'m'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorder">
        <xsl:with-param name="attrValue" select="@border"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapWidth">
        <xsl:with-param name="attrValue" select="@width"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorderColor">
        <xsl:with-param name="attrValue" select="@bordercolor"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapRounded">
        <xsl:with-param name="attrValue" select="@rounded"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapOutputClass">
        <xsl:with-param name="attrValue" select="@outputclass"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapDirection"/>
      <xsl:if test="not(@outputclass)">
        <xsl:attribute name="font-size">15pt</xsl:attribute>
      </xsl:if>
      <xsl:apply-templates/>
    </fo:block>
  </xsl:template>

  <!-- Text/Paragraphs within Blockquote (lq) -->
  <xsl:template match="*[contains(@class, ' topic/lq ')]/*[contains(@class, ' topic/p ')]" priority="5">
    <fo:block xsl:use-attribute-sets="p">
      <xsl:call-template name="commonattributes"/>
      
      <!-- Default large font for lq/p unless an override is present -->
      <xsl:if test="not(@outputclass) and not(parent::*/@outputclass)">
        <xsl:attribute name="font-size">15pt</xsl:attribute>
      </xsl:if>

      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@padding"/>
        <xsl:with-param name="prefix" select="'p'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapSpacing">
        <xsl:with-param name="attrValue" select="@margin"/>
        <xsl:with-param name="prefix" select="'m'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorder">
        <xsl:with-param name="attrValue" select="@border"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapWidth">
        <xsl:with-param name="attrValue" select="@width"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorderColor">
        <xsl:with-param name="attrValue" select="@bordercolor"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapRounded">
        <xsl:with-param name="attrValue" select="@rounded"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapOutputClass">
        <xsl:with-param name="attrValue" select="@outputclass"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapDirection"/>
      <xsl:attribute name="line-height">1.5</xsl:attribute>
      <xsl:apply-templates/>
    </fo:block>
  </xsl:template>

  <!-- Unstyled List Support (ul and ol) -->
  <xsl:template
    match="*[contains(@class, ' topic/ul ') or contains(@class, ' topic/ol ')][tokenize(@outputclass, ' ') = 'list-unstyled']"
  >
    <fo:block margin-bottom="12pt">
      <xsl:call-template name="commonattributes"/>
      <xsl:call-template name="processBootstrapDirection"/>
      <xsl:apply-templates select="*[contains(@class, ' topic/li ')]" mode="list-unstyled"/>
    </fo:block>
  </xsl:template>

  <xsl:template match="*[contains(@class, ' topic/li ')]" mode="list-unstyled">
    <fo:block margin-left="0pt" margin-bottom="3pt">
      <xsl:call-template name="commonattributes"/>
      <xsl:apply-templates/>
    </fo:block>
  </xsl:template>

  <!-- Inline List Support (ul and ol) -->
  <xsl:template
    match="*[contains(@class, ' topic/ul ') or contains(@class, ' topic/ol ')][tokenize(@outputclass, ' ') = 'list-inline']"
  >
    <fo:block margin-bottom="12pt">
      <xsl:call-template name="commonattributes"/>
      <xsl:call-template name="processBootstrapDirection"/>
      <xsl:apply-templates select="*[contains(@class, ' topic/li ')]" mode="list-inline"/>
    </fo:block>
  </xsl:template>

  <xsl:template match="*[contains(@class, ' topic/li ')]" mode="list-inline">
    <fo:inline padding-right="8pt">
      <xsl:call-template name="commonattributes"/>
      <xsl:apply-templates/>
    </fo:inline>
  </xsl:template>

  <!-- Suppress any elements used for dark/light mode switching in print -->
  <xsl:template
    match="*[tokenize(normalize-space(@outputclass), ' ') = 'd-light' or tokenize(normalize-space(@outputclass), ' ') = 'd-dark']"
    priority="5"
  />

  <!-- Only render the first image within a picture element -->
  <xsl:template match="*[contains(@class, ' bootstrap-d/picture ')]" priority="6">
    <fo:block>
      <xsl:call-template name="commonattributes"/>
      <xsl:apply-templates select="*[contains(@class, ' topic/image ')][1]"/>
    </fo:block>
  </xsl:template>

  <!-- Thumbnail Support -->
  <xsl:template match="*[contains(@class, ' bootstrap-d/thumbnail ')]" priority="6">
    <xsl:variable name="resolved-href">
      <xsl:choose>
        <xsl:when test="@scope = 'external' or opentopic-func:isAbsolute(@href)">
          <xsl:value-of select="@href"/>
        </xsl:when>
        <xsl:when test="exists(key('jobFile', @href, $job))">
          <xsl:value-of select="key('jobFile', @href, $job)/@src"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="concat($input.dir.url, @href)"/>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:variable>

    <xsl:variable name="theme" select="@color"/>
    <xsl:choose>
      <xsl:when test="@placement = 'break'">
        <fo:block margin-top="{$bootstrap-spacing-3}" margin-bottom="{$bootstrap-spacing-3}" text-align="center">
          <fo:external-graphic
            src="url('{$resolved-href}')"
            content-width="scale-to-fit"
            scaling="uniform"
            padding="{$bootstrap-spacing-1}"
            vertical-align="middle"
          >
            <xsl:call-template name="processBootstrapRounded">
              <xsl:with-param name="attrValue" select="(@rounded, '2')[1]"/>
            </xsl:call-template>
            <xsl:attribute name="border">
              <xsl:value-of select="concat($bootstrap-border-width, ' solid')"/>
            </xsl:attribute>
            <xsl:choose>
              <xsl:when test="$theme">
                <xsl:call-template name="processBootstrapBorderColor">
                  <xsl:with-param name="attrValue" select="$theme"/>
                </xsl:call-template>
                <xsl:call-template name="processBootstrapAttrSetReflection">
                  <xsl:with-param name="attrSet" select="concat('__bg__', $theme, '-subtle')"/>
                </xsl:call-template>
              </xsl:when>
              <xsl:otherwise>
                <xsl:attribute name="border-color"><xsl:value-of select="$bootstrap-border-color"/></xsl:attribute>
                <xsl:attribute name="background-color">#ffffff</xsl:attribute>
              </xsl:otherwise>
            </xsl:choose>
            <xsl:call-template name="commonattributes"/>
            <xsl:if test="@height"><xsl:attribute name="height" select="@height"/></xsl:if>
            <xsl:if test="@width"><xsl:attribute name="width" select="@width"/></xsl:if>
          </fo:external-graphic>
        </fo:block>
      </xsl:when>
      <xsl:otherwise>
        <fo:external-graphic
          src="url('{$resolved-href}')"
          content-width="scale-to-fit"
          scaling="uniform"
          padding="{$bootstrap-spacing-1}"
          vertical-align="middle"
        >
          <xsl:call-template name="processBootstrapRounded">
            <xsl:with-param name="attrValue" select="(@rounded, '2')[1]"/>
          </xsl:call-template>
          <xsl:attribute name="border">
            <xsl:value-of select="concat($bootstrap-border-width, ' solid')"/>
          </xsl:attribute>
          <xsl:choose>
            <xsl:when test="$theme">
              <xsl:call-template name="processBootstrapBorderColor">
                <xsl:with-param name="attrValue" select="$theme"/>
              </xsl:call-template>
              <xsl:call-template name="processBootstrapAttrSetReflection">
                <xsl:with-param name="attrSet" select="concat('__bg__', $theme, '-subtle')"/>
              </xsl:call-template>
            </xsl:when>
            <xsl:otherwise>
              <xsl:attribute name="border-color"><xsl:value-of select="$bootstrap-border-color"/></xsl:attribute>
              <xsl:attribute name="background-color">#ffffff</xsl:attribute>
            </xsl:otherwise>
          </xsl:choose>
          <xsl:call-template name="commonattributes"/>
          <xsl:if test="@height"><xsl:attribute name="height" select="@height"/></xsl:if>
          <xsl:if test="@width"><xsl:attribute name="width" select="@width"/></xsl:if>
        </fo:external-graphic>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <xsl:template match="*" mode="prismDecoration">
      <xsl:call-template name="processBootstrapAttrSetReflection">
          <xsl:with-param name="attrSet" select="'__bg__secondary-subtle'"/>
      </xsl:call-template>
      <xsl:call-template name="processBootstrapBorderColor">
          <xsl:with-param name="attrValue" select="'secondary'"/>
      </xsl:call-template>
      <!-- Overrides from settings-map if present -->
      <xsl:variable name="textColor">
          <xsl:call-template name="getBootstrapSetting">
              <xsl:with-param name="name" select="'prismjs.text.color'"/>
          </xsl:call-template>
      </xsl:variable>
      <xsl:variable name="bgColor">
          <xsl:call-template name="getBootstrapSetting">
              <xsl:with-param name="name" select="'prismjs.background.color'"/>
          </xsl:call-template>
      </xsl:variable>
      <xsl:variable name="borderWidth">
          <xsl:call-template name="getBootstrapSetting">
              <xsl:with-param name="name" select="'prismjs.border.width'"/>
          </xsl:call-template>
      </xsl:variable>
      <xsl:if test="$textColor != ''"><xsl:attribute name="color" select="$textColor"/></xsl:if>
      <xsl:if test="$bgColor != ''"><xsl:attribute name="background-color" select="$bgColor"/></xsl:if>
      <xsl:if test="$borderWidth != ''">
          <xsl:attribute name="border-width" select="$borderWidth"/>
          <xsl:if test="normalize-space($borderWidth) != ('0', '0pt', '0px', '0in', '0mm', '0cm', '0.0pt', '0.0px')">
              <xsl:attribute name="border-style">solid</xsl:attribute>
          </xsl:if>
      </xsl:if>
      <!-- Use global variables for consistent theme scaling and rounding awareness -->
      <xsl:call-template name="processBootstrapRounded">
        <xsl:with-param name="attrValue" select="(@rounded, 'yes')[1]"/>
      </xsl:call-template>
      <xsl:attribute name="padding"><xsl:value-of select="$bootstrap-spacing-1"/></xsl:attribute>
      <xsl:call-template name="bootstrap.decoration"/>
  </xsl:template>
  
  <xsl:template name="bootstrap.decoration">
      <xsl:apply-templates select="." mode="bootstrapDecoration"/>
  </xsl:template>

  <xsl:template match="*" mode="bootstrapDecoration">
      <!-- Default: No decoration -->
  </xsl:template>

  <!-- Remove borders -->
  <xsl:template name="bootstrapBorderless">
    <xsl:if test="not(@outline = 'yes' or @border or @bordercolor or contains(@outputclass, 'border') or contains(@class, ' bootstrap-d/card '))">
      <xsl:attribute name="border-width">0pt</xsl:attribute>
      <xsl:attribute name="border-style">none</xsl:attribute>
    </xsl:if>
  </xsl:template>

  <!-- Global Shadow Wrapper for shadow styling -->
  <xsl:template match="*[@shadow][not(@shadow = 'none') and not(@shadow = 'no')][not(contains(@class, ' bootstrap-d/card ') or tokenize(@outputclass, ' ') = 'card')]" priority="10">
    <xsl:variable name="inner">
      <xsl:next-match/>
    </xsl:variable>

    <xsl:call-template name="apply-shadow-wrapper">
      <xsl:with-param name="inner" select="$inner"/>
    </xsl:call-template>
  </xsl:template>

  <xsl:template name="apply-shadow-wrapper">
    <xsl:param name="inner"/>
    <xsl:param name="shadow-val" select="@shadow"/>
    <xsl:param name="margin-val" select="@margin"/>
    <!-- reset-indent: set to true() only when called from inside a table-cell context (e.g. cards)
         to anchor the wrapper at x=0 and prevent inherited body start-indent from causing a shift. -->
    <xsl:param name="reset-indent" select="false()"/>

    <xsl:choose>
      <xsl:when test="$inner/*[1][self::fo:inline or self::fo:basic-link]">
        <!-- Do not wrap inline elements in block-level shadow wrappers -->
        <xsl:copy-of select="$inner"/>
      </xsl:when>
      <xsl:otherwise>
        <xsl:variable name="shadow-offset">
          <xsl:choose>
            <xsl:when test="$shadow-val = 'sm'">3pt</xsl:when>
            <xsl:when test="$shadow-val = 'lg'">12pt</xsl:when>
            <xsl:when test="$shadow-val = 'md' or $shadow-val = 'yes'">6pt</xsl:when>
            <xsl:otherwise>6pt</xsl:otherwise>
          </xsl:choose>
        </xsl:variable>

        <!-- Bottom offset is half of right offset for a natural drop-shadow angle -->
        <xsl:variable name="shadow-offset-bottom">
          <xsl:choose>
            <xsl:when test="$shadow-val = 'sm'">1.5pt</xsl:when>
            <xsl:when test="$shadow-val = 'lg'">6pt</xsl:when>
            <xsl:otherwise>3pt</xsl:otherwise>
          </xsl:choose>
        </xsl:variable>

        <!-- Read the actual border-radius computed by the inner element's own template.
             This covers @rounded on the element itself, defaults applied by alert/note
             templates, and flat themes that set border-radius to 0. -->
        <xsl:variable name="inner-border-radius" select="$inner/*[1]/@fox:border-radius"/>

        <!-- Outer Boundary Restraint: perfectly inherits required structural dimensions and layout-padding.
             start-indent/end-indent reset to 0pt only when reset-indent=true() (e.g. card context inside
             a table-cell) to prevent inherited body indent from shifting shadow layers. -->
        <fo:block>
          <xsl:if test="$reset-indent">
            <xsl:attribute name="start-indent">0pt</xsl:attribute>
            <xsl:attribute name="end-indent">0pt</xsl:attribute>
          </xsl:if>
          <xsl:if test="$inner/*[1]/@width">
            <xsl:attribute name="width"><xsl:value-of select="$inner/*[1]/@width"/></xsl:attribute>
          </xsl:if>
          <xsl:if test="$inner/*[1]/@inline-progression-dimension">
            <xsl:attribute name="inline-progression-dimension"><xsl:value-of select="$inner/*[1]/@inline-progression-dimension"/></xsl:attribute>
          </xsl:if>
          
          <xsl:call-template name="processBootstrapSpacing">
            <xsl:with-param name="attrValue" select="$margin-val"/>
            <xsl:with-param name="prefix" select="'m'"/>
          </xsl:call-template>

          <!-- Expansion Sub-Wrapper: Expands outwards by the shadow-offset via negative structural margins.
               Bottom is halved relative to right for a more natural drop-shadow perspective. -->
          <fo:block margin-right="-{$shadow-offset}" margin-bottom="-{$shadow-offset-bottom}">
            <!-- Diffuse Shadow: layered nested blocks, light at the edge → dark near content.
                 sm (3pt right / 1.5pt bottom):  2 layers
                 md (6pt right / 3pt bottom):    6 layers, 1pt right steps
                 lg (12pt right / 6pt bottom):   8 layers, finer steps -->
            <xsl:choose>

              <!-- sm: 2 diffuse layers -->
              <xsl:when test="$shadow-val = 'sm'">
                <fo:block background-color="#eeeeee" padding-bottom="1.5pt" padding-right="3pt">
                  <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                  <fo:block background-color="#d4d4d4" padding-bottom="0.5pt" padding-right="1pt">
                    <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                    <xsl:apply-templates select="$inner/node()" mode="strip-margin"/>
                  </fo:block>
                </fo:block>
              </xsl:when>

              <!-- lg: 8 diffuse layers (12pt right / 6pt bottom) -->
              <xsl:when test="$shadow-val = 'lg'">
                <fo:block background-color="#f6f6f6" padding-bottom="6pt" padding-right="12pt">
                  <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                  <fo:block background-color="#f2f2f2" padding-bottom="5pt" padding-right="10pt">
                    <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                    <fo:block background-color="#eeeeee" padding-bottom="4pt" padding-right="8pt">
                      <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                      <fo:block background-color="#e8e8e8" padding-bottom="3pt" padding-right="6pt">
                        <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                        <fo:block background-color="#e0e0e0" padding-bottom="2pt" padding-right="4pt">
                          <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                          <fo:block background-color="#dcdcdc" padding-bottom="1.5pt" padding-right="3pt">
                            <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                            <fo:block background-color="#d8d8d8" padding-bottom="1pt" padding-right="2pt">
                              <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                              <fo:block background-color="#d4d4d4" padding-bottom="0.5pt" padding-right="1pt">
                                <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                                <xsl:apply-templates select="$inner/node()" mode="strip-margin"/>
                              </fo:block>
                            </fo:block>
                          </fo:block>
                        </fo:block>
                      </fo:block>
                    </fo:block>
                  </fo:block>
                </fo:block>
              </xsl:when>

              <!-- md / yes / default: 6 diffuse layers (6pt right / 3pt bottom, 1pt right steps) -->
              <xsl:otherwise>
                <fo:block background-color="#f4f4f4" padding-bottom="3pt" padding-right="6pt">
                  <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                  <fo:block background-color="#eeeeee" padding-bottom="2.5pt" padding-right="5pt">
                    <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                    <fo:block background-color="#e8e8e8" padding-bottom="2pt" padding-right="4pt">
                      <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                      <fo:block background-color="#e0e0e0" padding-bottom="1.5pt" padding-right="3pt">
                        <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                        <fo:block background-color="#d8d8d8" padding-bottom="1pt" padding-right="2pt">
                          <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                          <fo:block background-color="#d4d4d4" padding-bottom="0.5pt" padding-right="1pt">
                            <xsl:if test="$inner-border-radius"><xsl:attribute name="fox:border-radius"><xsl:value-of select="$inner-border-radius"/></xsl:attribute></xsl:if>
                            <xsl:apply-templates select="$inner/node()" mode="strip-margin"/>
                          </fo:block>
                        </fo:block>
                      </fo:block>
                    </fo:block>
                  </fo:block>
                </fo:block>
              </xsl:otherwise>

            </xsl:choose>
          </fo:block>
        </fo:block>
      </xsl:otherwise>
    </xsl:choose>

  </xsl:template>

  <!-- Identity transform to strip margins from the inner shadow block -->
  <xsl:template match="node() | @*" mode="strip-margin">
    <xsl:copy>
      <xsl:apply-templates select="node() | @*" mode="strip-margin"/>
    </xsl:copy>
  </xsl:template>

  <!-- Remove margin and explicit widths from the root fo:block of the processed inner tree -->
  <!-- For nested elements (non-root), all attributes pass through unchanged -->
  <xsl:template match="fo:block/@margin | fo:block/@margin-top | fo:block/@margin-bottom | fo:block/@margin-left | fo:block/@margin-right | fo:block/@width | fo:block/@inline-progression-dimension" mode="strip-margin">
    <xsl:if test="count(../ancestor::*) &gt; 1">
      <xsl:copy/>
    </xsl:if>
  </xsl:template>

  <!-- For fo:table: only strip margin/space attrs at root level, keep width -->
  <xsl:template match="fo:table/@margin | fo:table/@margin-top | fo:table/@margin-bottom | fo:table/@margin-left | fo:table/@margin-right | fo:table/@space-before | fo:table/@space-after" mode="strip-margin">
    <xsl:if test="count(../ancestor::*) &gt; 1">
      <xsl:copy/>
    </xsl:if>
  </xsl:template>

  <!-- Remove margin from root - for fo:block strip width too (outer block controls size) -->
  <xsl:template match="fo:block[count(ancestor::*) = 0]" mode="strip-margin" priority="6">
    <xsl:copy>
      <xsl:if test="not(@background-color)">
        <xsl:attribute name="background-color">#ffffff</xsl:attribute>
      </xsl:if>
      <xsl:attribute name="margin">0pt</xsl:attribute>
      <xsl:apply-templates select="node() | @*" mode="strip-margin"/>
    </xsl:copy>
  </xsl:template>

  <!-- For fo:table at root: only zero out margin/space; preserve width and border intact -->
  <xsl:template match="fo:table[count(ancestor::*) = 0]" mode="strip-margin" priority="6">
    <xsl:copy>
      <xsl:attribute name="margin">0pt</xsl:attribute>
      <xsl:attribute name="space-before">0pt</xsl:attribute>
      <xsl:attribute name="space-after">0pt</xsl:attribute>
      <xsl:apply-templates select="node() | @*" mode="strip-margin"/>
    </xsl:copy>
  </xsl:template>

</xsl:stylesheet>
